#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Анкета
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Анкета";
	КомандаПечати.Представление = НСтр("ru = 'Анкета'");
	КомандаПечати.Порядок = 1;
	
	// Транспортная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Транспортная накладная";
	КомандаПечати.Представление = НСтр("ru = 'Транспортная накладная'");
	КомандаПечати.Порядок = 2;
	
	// Договор на доставку
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Договор на доставку";
	КомандаПечати.Представление = НСтр("ru = 'Договор на доставку'");
	КомандаПечати.Картинка = БиблиотекаКартинок.ФорматWord2007;
	КомандаПечати.Порядок = 3;
	
	// Комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Анкета,Транспортная накладная";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов'");
	КомандаПечати.Порядок = 4;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Анкета");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАнкеты(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Анкета'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.дф_Доставка.ПФ_MXL_Анкета";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Транспортная накладная");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьТранспортнойНакладной(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Транспортная накладная'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.дф_Доставка.ПФ_MXL_ТранспортнаяНакладная";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Договор на доставку");
	Если ПечатнаяФорма <> Неопределено Тогда
		ОфисныеДокументы = ПечатьДоговорDOCX(МассивОбъектов);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Договор на доставку (MS Word)'");
		ПечатнаяФорма.ОфисныеДокументы = ОфисныеДокументы;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьАнкеты(МассивОбъектов, ОбъектыПечати)

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Анкета";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.дф_Доставка.ПФ_MXL_Анкета");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиТелоАнкеты(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьТранспортнойНакладной(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ТранспортнаяНакладная";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.дф_Доставка.ПФ_MXL_ТранспортнаяНакладная");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовокТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиШапкуТЧТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиТЧТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиПодвалТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
	
	Возврат ТабличныйДокумент;

	
КонецФункции

Функция ПечатьДоговорDOCX(МассивОбъектов)
	
    ОфисныеДокументы = Новый Соответствие;
    
    МакетДокумента = УправлениеПечатью.МакетПечатнойФормы("Документ.дф_Доставка.ПФ_DOC_ДоговорНаДоставку");
    Макет = УправлениеПечатью.ИнициализироватьМакетОфисногоДокумента(МакетДокумента, Неопределено);  
        
    ОписаниеОбластей = Новый Структура;
    УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Шапка", "Общая");
    УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Тело", "Общая");
    УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Подвал", "Общая");

    ДанныеДокументов = ПолучитьДанныеДокументовДляДоговора(МассивОбъектов);
    
	Пока ДанныеДокументов.Следующий() Цикл
		
		ПараметрыДокумента = Новый Структура;
		ПараметрыДокумента.Вставить("Дата", Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
		ПараметрыДокумента.Вставить("Номер", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер));
		ПараметрыДокумента.Вставить("Организация", ДанныеДокументов.Организация);
		ПараметрыДокумента.Вставить("Контрагент", ДанныеДокументов.Контрагент);
		ПараметрыДокумента.Вставить("АдресДоставки", ДанныеДокументов.АдресДоставки);
		ПараметрыДокумента.Вставить("ОрганизацияИНН", ДанныеДокументов.ОрганизацияИНН);
		ПараметрыДокумента.Вставить("ОрганизацияКПП", ДанныеДокументов.ОрганизацияКПП);
		ПараметрыДокумента.Вставить("КонтрагентИНН", ДанныеДокументов.КонтрагентИНН);
		ПараметрыДокумента.Вставить("КонтрагентКПП", ДанныеДокументов.КонтрагентКПП);
		ПараметрыДокумента.Вставить("НаименованиеКонтактногоЛица", ДанныеДокументов.НаименованиеКонтактногоЛица);
		ПараметрыДокумента.Вставить("ДолжностьКонтактногоЛица", ДанныеДокументов.ДолжностьКонтактногоЛица);
		ПараметрыДокумента.Вставить("СуммаДокумента", ДанныеДокументов.СуммаДокумента);

		ПечатнаяФорма = УправлениеПечатью.ИнициализироватьПечатнуюФорму(Неопределено, Неопределено, Макет);
        
        ОбластьШапка = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["Шапка"]);
        УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, ОбластьШапка, ПараметрыДокумента);
		
		ОбластьТело = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["Тело"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, ОбластьТело, ПараметрыДокумента);
		
		ОбластьПодвал = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["Подвал"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, ОбластьПодвал, ПараметрыДокумента);
		
		АдресХранилищаПечатнойФормы = УправлениеПечатью.СформироватьДокумент(ПечатнаяФорма);
        ОфисныеДокументы.Вставить(АдресХранилищаПечатнойФормы, Строка(ДанныеДокументов.Ссылка));
        УправлениеПечатью.ОчиститьСсылки(ПечатнаяФорма);
        
    КонецЦикла;        
    
    УправлениеПечатью.ОчиститьСсылки(Макет);
    
    Возврат ОфисныеДокументы;
    
КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	дф_Доставка.Ссылка КАК Ссылка,
	|	дф_Доставка.Номер КАК Номер,
	|	дф_Доставка.Дата КАК Дата,
	|	дф_Доставка.Организация КАК Организация,
	|	дф_Доставка.Контрагент КАК Контрагент,
	|	дф_Доставка.Товары.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		Количество КАК Количество
	|	) КАК Товары,
	|	дф_Доставка.АдресДоставки КАК АдресДоставки,
	|	дф_Доставка.Организация.ИНН КАК ОрганизацияИНН,
	|	дф_Доставка.Организация.КПП КАК ОрганизацияКПП,
	|	дф_Доставка.Контрагент.ИНН КАК КонтрагентИНН,
	|	дф_Доставка.Контрагент.КПП КАК КонтрагентКПП
	|ИЗ
	|	Документ.дф_Доставка КАК дф_Доставка
	|ГДЕ
	|	дф_Доставка.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Функция ПолучитьДанныеДокументовДляДоговора(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	дф_Доставка.Ссылка КАК Ссылка,
	|	дф_Доставка.Номер КАК Номер,
	|	дф_Доставка.Дата КАК Дата,
	|	дф_Доставка.Организация КАК Организация,
	|	дф_Доставка.Контрагент КАК Контрагент,
	|	дф_Доставка.Товары.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		Количество КАК Количество
	|	) КАК Товары,
	|	дф_Доставка.АдресДоставки КАК АдресДоставки,
	|	дф_Доставка.Организация.ИНН КАК ОрганизацияИНН,
	|	дф_Доставка.Организация.КПП КАК ОрганизацияКПП,
	|	дф_Доставка.Контрагент.ИНН КАК КонтрагентИНН,
	|	дф_Доставка.Контрагент.КПП КАК КонтрагентКПП,
	|	дф_КонтактныеЛица.Наименование КАК НаименованиеКонтактногоЛица,
	|	дф_КонтактныеЛица.Должность КАК ДолжностьКонтактногоЛица,
	|	дф_Доставка.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.дф_Доставка КАК дф_Доставка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.дф_КонтактныеЛица КАК дф_КонтактныеЛица
	|		ПО дф_Доставка.Контрагент = дф_КонтактныеЛица.Владелец
	|ГДЕ
	|	дф_Доставка.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Процедура ВывестиТелоАнкеты(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьТелоАнкеты = Макет.ПолучитьОбласть("Тело");
	
	ДанныеПечати = Новый Структура;
	
	ШаблонЗаголовка = "Анкета о доставке №%1 от %2";
	ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
		Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
	
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(ДанныеДокументов.Ссылка, 1, 120);
	
	Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
		|Технические подробности см. в журнале регистрации.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	Иначе
		КартинкаQRКода = Новый Картинка(ДанныеQRКода);
		ОбластьТелоАнкеты.Рисунки.МашиночитаемыйКод.Картинка = КартинкаQRКода;
	КонецЕсли;
	
	ОбластьТелоАнкеты.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьТелоАнкеты);
	
КонецПроцедуры

Процедура ВывестиЗаголовокТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьЗаголовокТранспортнойНакладной = Макет.ПолучитьОбласть("Заголовок");
	
	ДанныеПечати = Новый Структура;
	
	ШаблонЗаголовка = "Транспортная накладная №%1 от %2";
	ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
		Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
	ДанныеПечати.Вставить("Организация", ДанныеДокументов.Организация);
	ДанныеПечати.Вставить("Контрагент", ДанныеДокументов.Контрагент);
	
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(ДанныеДокументов.Ссылка, 1, 120);
	
	Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
		|Технические подробности см. в журнале регистрации.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	Иначе
		КартинкаQRКода = Новый Картинка(ДанныеQRКода);
		ОбластьЗаголовокТранспортнойНакладной.Рисунки.МашиночитаемыйКод.Картинка = КартинкаQRКода;
	КонецЕсли;

	ОбластьЗаголовокТранспортнойНакладной.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьЗаголовокТранспортнойНакладной);

КонецПроцедуры

Процедура ВывестиШапкуТЧТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьШапкиТЧТранспортнойНакладной = Макет.ПолучитьОбласть("ШапкаТЧ");
	
	ТабличныйДокумент.Вывести(ОбластьШапкиТЧТранспортнойНакладной);

КонецПроцедуры

Процедура ВывестиТЧТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьТЧТранспортнойНакладной = Макет.ПолучитьОбласть("ТЧ");
	
	ВыборкаТовары = ДанныеДокументов.Товары.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		ОбластьТЧТранспортнойНакладной.Параметры.Заполнить(ВыборкаТовары);
		ТабличныйДокумент.Вывести(ОбластьТЧТранспортнойНакладной);
	КонецЦикла;

КонецПроцедуры

Процедура ВывестиПодвалТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьПодвалТранспортнойНакладной = Макет.ПолучитьОбласть("Подвал");
	
	ТабличныйДокумент.Вывести(ОбластьПодвалТранспортнойНакладной);

КонецПроцедуры

#КонецОбласти

#КонецЕсли